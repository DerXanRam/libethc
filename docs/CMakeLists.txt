FIND_PACKAGE(Doxygen REQUIRED)
FIND_PACKAGE(Python REQUIRED)

EXECUTE_PROCESS(
  COMMAND ${Python_EXECUTABLE} -c "import sphinx;import breathe"
  RESULT_VARIABLE SPHINX_BREATHE_RETURN)

IF (NOT SPHINX_BREATHE_RETURN EQUAL "0")
  MESSAGE(FATAL_ERROR "ethc: could not find sphinx or breathe (or both)")
ENDIF()

SET(DOXYGEN_PROJECT_NAME ${CMAKE_PROJECT_NAME})
SET(DOXYGEN_PROJECT_NUMBER ${CMAKE_PROJECT_VERSION})

SET(DOXYGEN_INPUT_DIRECTORY ${PROJECT_SOURCE_DIR}/include)
SET(DOXYGEN_OUTPUT_DIRECTORY ${CMAKE_CURRENT_BINARY_DIR}/doxygen)

SET(DOXYGEN_HTML_OUTPUT_DIRECTORY ${DOXYGEN_OUTPUT_DIRECTORY}/html)
SET(DOXYGEN_INDEX_FILE ${DOXYGEN_OUTPUT_DIRECTORY}/xml/index.xml)
SET(DOXYGEN_GENERATE_HTML NO)
SET(DOXYGEN_GENERATE_XML YES)
SET(DOXYGEN_ENABLE_PREPROCESSING YES)
SET(DOXYGEN_MACRO_EXPANSION YES)
SET(DOXYGEN_EXPAND_ONLY_PREDEF YES)
SET(DOXYGEN_PREDEFINED ETHC_EXPORT)
SET(DOXYGEN_INTERNAL_DOCS NO)
SET(DOXYGEN_OPTIMIZE_OUTPUT_FOR_C YES)

DOXYGEN_ADD_DOCS(doxygen ${DOXYGEN_INPUT_DIRECTORY})

SET(SPHINX_SOURCE ${CMAKE_CURRENT_BINARY_DIR})
SET(SPHINX_BUILD ${CMAKE_BINARY_DIR}/sphinx/html)
SET(SPHINX_INDEX_FILE ${SPHINX_BUILD}/index.html)

CONFIGURE_FILE(${CMAKE_CURRENT_SOURCE_DIR}/conf.py.in ${CMAKE_CURRENT_BINARY_DIR}/conf.py)

SET(RST_FILES ${CMAKE_CURRENT_SOURCE_DIR}/*.rst)

ADD_CUSTOM_COMMAND(OUTPUT ${SPHINX_INDEX_FILE}
  COMMAND ${CMAKE_COMMAND} -E copy_if_different ${RST_FILES} ${CMAKE_CURRENT_BINARY_DIR}    
  COMMAND  ${Python_EXECUTABLE} -m sphinx ${SPHINX_SOURCE} ${SPHINX_BUILD}
  WORKING_DIRECTORY ${CMAKE_CURRENT_BINARY_DIR}
  DEPENDS
  ${RST_FILES}
  doxygen
  MAIN_DEPENDENCY ${CMAKE_CURRENT_SOURCE_DIR}/conf.py.in
  COMMENT "Generating HTML documentation with Sphinx into ${CMAKE_BINARY_DIR}/sphinx/html/")

ADD_CUSTOM_TARGET(sphinx DEPENDS ${SPHINX_INDEX_FILE})
